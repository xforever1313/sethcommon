import SCons.Defaults
import SCons
from SCons.Util import unique
import stat
import os
import sys
import subprocess
import glob
import shutil
from copy import deepcopy

baseDir = os.path.abspath("../../")
sys.path.append(baseDir) #Get globals
from Globals import *
from SethCommonGlobals import *
from SConstructGlobals import *

#Make common environment
projectName = "SethOS"
targetFlags = CREATE_LIB_TARGET | DEBUG_RELEASE_LIB_TARGET | DOXYGEN_TARGET | CPP_CHECK_TARGET | TESTING_TARGET | PROJECT_TARGET
envBase = createBaseEnvironment(baseDir, baseDir, projectName, targetFlags, ARGUMENTS)

###
# Files to compile
###

###include paths:
includePaths = [includeDir, getDateVersionPath(baseDir), os.path.join(getCommonClassesPath(baseDir), includeDir)]
testIncludePaths = [os.path.join(testDir, includeDir)]

###Source Files
dateVersionFile = (os.path.join(srcDir, 'OSDateVersion.cpp'))
sourceFiles = filterSourceFiles(glob.glob(os.path.join(srcDir, "*.cpp")) +
                                glob.glob(os.path.join(srcDir, "*/*.cpp")), [dateVersionFile])
sourceTestFiles = glob.glob(os.path.join(testDir, "*.cpp")) + glob.glob(os.path.join(testDir, "*/*.cpp")) + [getTestMain(baseDir)]
coverageFiles = deepcopy(sourceFiles + [dateVersionFile])

###Libs
libs = []
testLibs = [] + libs
if (not envBase['ASM_JS_BUILD'] and not envBase['MSVC_BUILD']):
    testLibs += ["pthread"]

###libPath
libPath = []

Help(getHelpMessage(envBase))

###
# Environment creation
###

def addFlags(env):
    if (env['SERVER_BUILD']):
        # Removes this flag from Travis-CI builds.
        env.Append(CCFLAGS = '-Wno-unused-result')

#Debug Environment
debugEnvironment = createDebugEnvironment(envBase, includePaths, libs, libPath)
addFlags(debugEnvironment)

#Release Environment
releaseEnvironment = createReleaseEnvironment(envBase, includePaths, libs, libPath)
addFlags(releaseEnvironment)

#Unit test environment
testEnvironment = createUnitTestEnvironment(envBase, includePaths + testIncludePaths, testLibs, libPath)
addFlags(testEnvironment)

#Remove debug_new from shared builds.  On windows this causes stuff
#to crash for some reason.  To check for memory leaks, use static
#Builds.
if (envBase['SHARED_BUILD']):
    if ('debug_new') in testEnvironment['LIBS']:
        testEnvironment['LIBS'].remove('debug_new')

###
# Test outputs
###
createTestOutputFolder(envBase)

#make unEditableFile.txt and uneditableDir unwritable, and unReadableFile.txt unreadable
try:
    os.chmod(os.path.join(".", "Test", "unEditableFile.txt"), stat.S_IREAD)
    os.chmod(os.path.join(".", "Test", "unReadableFile.txt"), 0)
except OSError:
    pass

###
# Targets
###

debugTarget = createStaticLib(debugEnvironment, "SethOS-d", getCompiledObjectsWithDateVersionObject(debugEnvironment, sourceFiles, dateVersionFile, ARGUMENTS))
releaseTarget = createStaticLib(releaseEnvironment, "SethOS", getCompiledObjectsWithDateVersionObject(releaseEnvironment, sourceFiles, dateVersionFile, ARGUMENTS))
(unitTestTarget, runTestTarget) = createUnitTestExe(testEnvironment, getCompiledObjectsWithDateVersionObject(testEnvironment, sourceFiles + sourceTestFiles, dateVersionFile, ARGUMENTS), coverageFiles)
cppCheckTarget = createCppCheckTarget(releaseEnvironment, coverageFiles)

otherIncludeFiles = [os.path.join(getCppUTestPath(baseDir), includeDir), os.path.join(getGMockPath(baseDir), includeDir)]
(netBeansTarget, vsTarget, codeBlocksTarget) = createProjectTargets(envBase, testEnvironment['CPPPATH'] + otherIncludeFiles, "")

doxygenTarget = createDoxygenTarget(envBase, otherIncludeFiles + testEnvironment['CPPPATH'], 'Abstracted out OS calls')

Alias(DEBUG_LIB_ALIAS, debugTarget)
Alias(RELEASE_LIB_ALIAS, releaseTarget)
Alias(CREATE_LIB_ALIAS, [debugTarget, releaseTarget])
Alias(DOXYGEN_ALIAS, doxygenTarget)
Alias(CPP_CHECK_ALIAS, cppCheckTarget)
Alias(UNIT_TEST_ALIAS, unitTestTarget)
Alias(RUN_TEST_ALIAS, [unitTestTarget, cppCheckTarget, runTestTarget])

Alias (NIGHTLY_ALIAS, [debugTarget, releaseTarget, doxygenTarget, cppCheckTarget, runTestTarget, netBeansTarget, vsTarget, codeBlocksTarget])
Alias (DELTA_ALIAS, [debugTarget, releaseTarget, cppCheckTarget, runTestTarget])

Default([RUN_TEST_ALIAS])
