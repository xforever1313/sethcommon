import SCons.Defaults
import SCons
from SCons.Util import unique
import stat
import os
import sys
import subprocess
import glob
import shutil
from copy import deepcopy

baseDir = "../../"
sys.path.append(baseDir) #Get globals
from Globals import *
from SConstructGlobals import *

targetFlags = (CREATE_LIB_TARGET | TESTING_TARGET | DOXYGEN_TARGET)
projectName = "CppUTest"
envBase = createBaseEnvironment(baseDir, baseDir, projectName, targetFlags, ARGUMENTS)

#include paths
includePaths = [".", "include"]

#source files
sourceDirectories = ["src/CppUTest", "src/CppUTest/Extensions"]
if (envBase['MSVC_BUILD']):
    sourceDirectories += ["src/Platforms/VisualCpp"]
else:    
    sourceDirectories += ["src/Platforms/Gcc"]

sourceTestDirectories = ["tests", "tests/Extensions"]

sourceFiles = []
testSourceFiles = []

#Add all .cpp files
for dir in sourceDirectories:
    sourceFiles += glob.glob(os.path.join(dir, "*.cpp"))
for dir in sourceTestDirectories:
    testSourceFiles += glob.glob(os.path.join(dir, "*.cpp"))

coverageFiles = deepcopy(sourceFiles) #Creates a copy of the list
    
#Libs
libs = []
testLibs = ["CppUTest"]

#libPath
libPath = []
testLibPath = [os.path.join(libDir, envBase['SYSTEM'])]

#link flags
linkFlags = []

###
# Environment creation
###
libEnvironment = createDebugEnvironment(envBase, includePaths, libs, libPath)
#Override flags
libEnvironment.Append(CPPDEFINES = ["UT_NEW_MACROS_DISABLED", "UT_NEW_OVERRIDES_DISABLED"])

if (envBase['MSVC_BUILD']):
    libEnvironment['CCFLAGS'].remove('/WX')
    libEnvironment.Append(CPPDEFINES = ['_CRT_SECURE_NO_WARNINGS'])
else:
    libEnvironment['CCFLAGS'].remove('-Werror')
    libEnvironment['CCFLAGS'].remove('-pedantic-errors')

libEnvironment['LIBDIR'] = os.path.join(libDir, envBase['SYSTEM'])

testEnvironment = createUnitTestEnvironment(envBase, includePaths, testLibs, testLibPath)
#Override flags
if (envBase['MSVC_BUILD']):
    testEnvironment['CCFLAGS'].remove('/WX')
else:
    testEnvironment['CCFLAGS'].remove('-Werror')
    testEnvironment['CCFLAGS'].remove('-pedantic-errors')


if (not testEnvironment['CLANG_BUILD'] and not testEnvironment['MSVC_BUILD']):
    testEnvironment.Append(CCFLAGS = ["-fprofile-arcs", "-ftest-coverage"])

#Doxygen 
doxygenFiles = sourceFiles + testSourceFiles + glob.glob(os.path.join(includeDir, "*h")) +  glob.glob(os.path.join(includeDir, "*", "*h"))

Help(getHelpMessage(envBase))

###
# Targets
###
libBuild  = createStaticLib(libEnvironment, "CppUTest", sourceFiles)
(unitTestBuild, runUnitTests) = createUnitTestExe(testEnvironment, testSourceFiles + sourceFiles, coverageFiles)

doxygenTarget = createDoxygenTarget(envBase, doxygenFiles)

Alias("create_lib", libBuild)
Alias("doxygen", doxygenTarget)
Alias("unit_test", unitTestBuild)
Alias("run_unit_test", runUnitTests)
Alias("run_test", runUnitTests)

Alias ("nightly", ["doxygen", "create_lib"])
Alias ("delta", ["create_lib"])

Default("create_lib")
